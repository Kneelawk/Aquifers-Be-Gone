plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'maven-publish'
    id 'idea'
}

archivesBaseName = "${mod_name}-fabric-${minecraft_version}"

libsDirectory.set(new File(rootProject.buildDir, "libs"))

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    implementation project(":Common")
    implementation project(path: ":Fabric", configuration: "namedElements")
    implementation project(":Common-${minecraft_version}")

    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
}

loom {
    mixin.defaultRefmapName = "aquifersbegone.common.refmap.json"

    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}


processResources {
    from project(":Common").sourceSets.main.resources
    from project(":Fabric").sourceSets.main.resources
    from project(":Common-${minecraft_version}").sourceSets.main.resources

    inputs.property "version", project.version
    inputs.property "minecraft_version_constraint", minecraft_version_constraint

    filesMatching("fabric.mod.json") {
        expand "version": project.version, "minecraft_version_constraint": minecraft_version_constraint
    }
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
    source(project(":Fabric").sourceSets.main.allSource)
    source(project(":Common-${minecraft_version}").sourceSets.main.allSource)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
